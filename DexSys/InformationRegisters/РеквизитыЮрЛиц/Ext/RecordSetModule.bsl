
Процедура ПередЗаписью(Отказ, Замещение)
	
	Если Не ЭтотОбъект.ДополнительныеСвойства.Свойство("ПропуститьПроверкуФилиалы") Тогда
		ПроверитьЕстьЛиВНабореФилиалы(Отказ);
	КонецЕсли; // Свойство("ПропуститьПроверкуФилиалы")
	
	Если Не Отказ И ЭтотОбъект.Модифицированность() Тогда
		
		Если ЭтотОбъект.ДополнительныеСвойства.Свойство("СоздатьДочерниеЗаписи") Тогда
			
			Если ЭтотОбъект.ДополнительныеСвойства.СоздатьДочерниеЗаписи Тогда
				РегистрыСведений.РеквизитыЮрЛиц.СоздатьДочерниеЗаписи(ЭтотОбъект);
			КонецЕсли; // ДочерниеЗаписи
			
		КонецЕсли; // Свойство("ДочерниеЗаписи")
		
	КонецЕсли; // Не Отказ И ЭтотОбъект.Модифицированность()
	
КонецПроцедуры

Процедура ПроверитьЕстьЛиВНабореФилиалы(Отказ)
	
	Если ЭтотОбъект.Количество() > 0 Тогда
	
		Запрос = Новый Запрос;
		Запрос.Текст = "ВЫБРАТЬ РАЗЛИЧНЫЕ
		               |	ТЗ.ЮрЛицо КАК ЮрЛицо
		               |ПОМЕСТИТЬ ВТ_ТЗ
		               |ИЗ
		               |	&ТЗ КАК ТЗ
		               |;
		               |
		               |////////////////////////////////////////////////////////////////////////////////
		               |ВЫБРАТЬ
		               |	ЮрЛица.Ссылка КАК Ссылка
		               |ИЗ
		               |	Справочник.ЮрЛица КАК ЮрЛица
		               |ГДЕ
		               |	НЕ ЮрЛица.ГоловнаяОрганизация = ЗНАЧЕНИЕ(Справочник.ЮрЛица.ПустаяСсылка)
		               |	И ЮрЛица.Ссылка В
		               |			(ВЫБРАТЬ
		               |				ВТ_ТЗ.ЮрЛицо
		               |			ИЗ
		               |				ВТ_ТЗ)";
		Запрос.УстановитьПараметр("ТЗ", ЭтотОбъект.Выгрузить());
		Результат = Запрос.Выполнить();
		
		Если Не Результат.Пустой() Тогда
			РегистрыСведений.РеквизитыЮрЛиц.ОтказФилиал(Отказ);
		Иначе
			ЭтотОбъект.ДополнительныеСвойства.Вставить("СоздатьДочерниеЗаписи", Истина);
		КонецЕсли;
		
	Иначе
		
		РегистрыСведений.РеквизитыЮрЛиц.УдалитьДочерниеЗаписи(ЭтотОбъект, Отказ);
		
	КонецЕсли; // ЭтотОбъект.Количество() > 0
	
КонецПроцедуры



