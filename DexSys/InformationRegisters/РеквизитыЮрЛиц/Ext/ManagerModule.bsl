
Процедура СоздатьДочерниеЗаписи(НаборЗаписей) Экспорт
	
	Если Не НаборЗаписей.Количество() = 0 Тогда
		
		Запрос = Новый Запрос;
		Запрос.Текст = "ВЫБРАТЬ РАЗЛИЧНЫЕ
						|	ТЗ.Период КАК Период,
						|	ТЗ.ИНН КАК ИНН,
						|	ТЗ.ЮрЛицо КАК ГоловнаяОрганизация
						|ПОМЕСТИТЬ ВТ_Набор
						|ИЗ
						|	&ТЗ КАК ТЗ
						|;
						|
						|////////////////////////////////////////////////////////////////////////////////
						|ВЫБРАТЬ
						|	ЮрЛица.Ссылка КАК Филиал,
						|	ЮрЛица.ГоловнаяОрганизация КАК ГоловнаяОрганизация
						|ПОМЕСТИТЬ ВТ_Филиалы
						|ИЗ
						|	Справочник.ЮрЛица КАК ЮрЛица
						|ГДЕ
						|	ЮрЛица.ГоловнаяОрганизация В
						|			(ВЫБРАТЬ
						|				ВТ_Набор.ГоловнаяОрганизация
						|			ИЗ
						|				ВТ_Набор)
						|;
						|
						|////////////////////////////////////////////////////////////////////////////////
						|ВЫБРАТЬ
						|	РеквизитыЮрЛиц.Период КАК Период,
						|	РеквизитыЮрЛиц.ЮрЛицо КАК ЮрЛицо,
						|	РеквизитыЮрЛиц.ИНН КАК ИНН
						|ПОМЕСТИТЬ ВТ_Реквизиты
						|ИЗ
						|	РегистрСведений.РеквизитыЮрЛиц КАК РеквизитыЮрЛиц
						|ГДЕ
						|	РеквизитыЮрЛиц.Период В
						|			(ВЫБРАТЬ
						|				ВТ_Набор.Период
						|			ИЗ
						|				ВТ_Набор)
						|	И РеквизитыЮрЛиц.ЮрЛицо В
						|			(ВЫБРАТЬ
						|				ВТ_Филиалы.Филиал
						|			ИЗ
						|				ВТ_Филиалы)
						|;
						|
						|////////////////////////////////////////////////////////////////////////////////
						|ВЫБРАТЬ
						|	ВТ_Филиалы.Филиал КАК Филиал,
						|	ВТ_Филиалы.ГоловнаяОрганизация КАК ГоловнаяОрганизация,
						|	ВТ_Реквизиты.Период КАК Период,
						|	ВТ_Реквизиты.ИНН КАК ИННФилиал,
						|	ВТ_Набор.ГоловнаяОрганизация КАК НаборГоловнаяОрганизация,
						|	ВТ_Набор.ИНН КАК ИННГоловнаяОрганизация,
						|	ВТ_Набор.Период КАК НаборПериод
						|ИЗ
						|	ВТ_Филиалы КАК ВТ_Филиалы
						|		ЛЕВОЕ СОЕДИНЕНИЕ ВТ_Набор КАК ВТ_Набор
						|		ПО ВТ_Филиалы.ГоловнаяОрганизация = ВТ_Набор.ГоловнаяОрганизация
						|		ЛЕВОЕ СОЕДИНЕНИЕ ВТ_Реквизиты КАК ВТ_Реквизиты
						|		ПО ВТ_Филиалы.Филиал = ВТ_Реквизиты.ЮрЛицо
						|ГДЕ
						|	ВТ_Реквизиты.ИНН ЕСТЬ NULL
						|ИТОГИ ПО
						|	НаборГоловнаяОрганизация,
						|	ИННГоловнаяОрганизация,
						|	НаборПериод
						|";
		Запрос.УстановитьПараметр("ТЗ", НаборЗаписей.Выгрузить());
		Результат = Запрос.Выполнить();
		
		Если Не Результат.Пустой() Тогда
			СоздатьДочерниеЗаписиИзЗапроса(Результат);
		КонецЕсли;
		
	КонецЕсли;
	
КонецПроцедуры

Процедура СоздатьДочерниеЗаписиИзЗапроса(РезультатЗапроса)
	
	ВыборкаГоловнаяОрганизация = РезультатЗапроса.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
	
	Пока ВыборкаГоловнаяОрганизация.Следующий() Цикл
		ВыборкаИННГоловнаяОрганизация = ВыборкаГоловнаяОрганизация.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
		
		Пока ВыборкаИННГоловнаяОрганизация.Следующий() Цикл
			
			ВыборкаНаборПериод = ВыборкаИННГоловнаяОрганизация.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
			
			Пока ВыборкаНаборПериод.Следующий() Цикл
				
				Выборка = ВыборкаНаборПериод.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
				НаборЗаписей = РегистрыСведений.РеквизитыЮрЛиц.СоздатьНаборЗаписей();
				
				НаборЗаписей.ДополнительныеСвойства.Вставить("ПропуститьПроверкуФилиалы",Истина);
				НаборЗаписей.ДополнительныеСвойства.Вставить("ДочерниеЗаписи",Ложь);
				
				НаборЗаписей.Отбор.Период.Установить(ВыборкаНаборПериод.НаборПериод);
				НаборЗаписей.Прочитать();
				
				Пока Выборка.Следующий() Цикл
					
					Запись = НаборЗаписей.Добавить();
					Запись.ЮрЛицо = Выборка.Филиал;
					Запись.Период = Выборка.НаборПериод;
					Запись.ИНН = Выборка.ИННГоловнаяОрганизация;
					
				КонецЦикла; // Выборка
				
				НаборЗаписей.Записать();
				
			КонецЦикла; // ВыборкаНаборПериод
			
		КонецЦикла; // ВыборкаИННГоловнаяОрганизация
		
	КонецЦикла; // ВыборкаГоловнаяОрганизация
	
КонецПроцедуры

Процедура УдалитьДочерниеЗаписи(НаборЗаписей, Отказ) Экспорт
	
	ЮрЛицо = НаборЗаписей.Отбор.ЮрЛицо.Значение;
		
	Если ОбщегоНазначения.ЭтоФилиал(ЮрЛицо) Тогда
		ОтказФилиал(Отказ);
	Иначе
		УдалитьЗаписиФилиала(НаборЗаписей);
	КонецЕсли;
	
КонецПроцедуры

Процедура ОтказФилиал(Отказ) Экспорт
	Отказ = Истина;
	Сообщить("Изменение ИНН филиала может быть инициировано только при изменении головной организации!");
КонецПроцедуры

Процедура УдалитьЗаписиФилиала(НаборЗаписей)
	
	ГоловнаяОрганизация = НаборЗаписей.Отбор.ЮрЛицо.Значение;
	Период = НаборЗаписей.Отбор.Период.Значение;
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	ЮрЛица.Ссылка КАК Филиал
		|ПОМЕСТИТЬ ВТ_Филиалы
		|ИЗ
		|	Справочник.ЮрЛица КАК ЮрЛица
		|ГДЕ
		|	ЮрЛица.ГоловнаяОрганизация = &ГоловнаяОрганизация
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	РеквизитыЮрЛиц.ЮрЛицо КАК ЮрЛицо
		|ИЗ
		|	РегистрСведений.РеквизитыЮрЛиц КАК РеквизитыЮрЛиц
		|ГДЕ
		|	РеквизитыЮрЛиц.Период = &Период
		|	И РеквизитыЮрЛиц.ЮрЛицо В
		|			(ВЫБРАТЬ
		|				ВТ_Филиалы.Филиал КАК Филиал
		|			ИЗ
		|				ВТ_Филиалы КАК ВТ_Филиалы)";
	
	Запрос.УстановитьПараметр("ГоловнаяОрганизация", ГоловнаяОрганизация);
	Запрос.УстановитьПараметр("Период", Период);
	
	РезультатЗапроса = Запрос.Выполнить();
	
	ВыборкаДетальныеЗаписи = РезультатЗапроса.Выбрать();
	
	Пока ВыборкаДетальныеЗаписи.Следующий() Цикл
		НаборЗаписейУдаление = РегистрыСведений.РеквизитыЮрЛиц.СоздатьНаборЗаписей();
		НаборЗаписейУдаление.ДополнительныеСвойства.Вставить("ПропуститьПроверкуФилиалы",Истина);
		НаборЗаписейУдаление.ДополнительныеСвойства.Вставить("ДочерниеЗаписи",Ложь);
		НаборЗаписейУдаление.Отбор.Период.Установить(Период);
		НаборЗаписейУдаление.Отбор.ЮрЛицо.Установить(ВыборкаДетальныеЗаписи.ЮрЛицо);
		НаборЗаписейУдаление.Прочитать();
		НаборЗаписейУдаление.Очистить();
		НаборЗаписейУдаление.Записать();
	КонецЦикла;
	
КонецПроцедуры