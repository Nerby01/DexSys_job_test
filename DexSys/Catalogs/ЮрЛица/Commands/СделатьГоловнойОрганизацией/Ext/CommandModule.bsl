
&НаКлиенте
Процедура ОбработкаКоманды(ПараметрКоманды, ПараметрыВыполненияКоманды)
	
	ЮрЛицо = ПараметрКоманды;
	ОбработкаКомандыНаСервере(ЮрЛицо);
	
КонецПроцедуры


&НаСервере
Процедура ОбработкаКомандыНаСервере(ЮрЛицо)
	
	Если Не ОбщегоНазначения.ЭтоФилиал(ЮрЛицо) Тогда
		Сообщить("Юр лицо уже является головной организацией");
		Возврат;
	КонецЕсли;
	
	Филиалы = ПолучитьНовыйСписокФилиалов(ЮрЛицо);
	
	ИзменитьИерархию(ЮрЛицо,Филиалы);
	
КонецПроцедуры

&НаСервере
Функция ПолучитьНовыйСписокФилиалов(ЮрЛицо)
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	ЮрЛица.ГоловнаяОрганизация КАК ГоловнаяОрганизация
		|ПОМЕСТИТЬ ВТ_ГоловнаяОрганизация
		|ИЗ
		|	Справочник.ЮрЛица КАК ЮрЛица
		|ГДЕ
		|	ЮрЛица.Ссылка = &ЮрЛицо
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ЮрЛица.Ссылка КАК Филиал
		|ИЗ
		|	Справочник.ЮрЛица КАК ЮрЛица
		|ГДЕ
		|	ЮрЛица.ГоловнаяОрганизация В
		|			(ВЫБРАТЬ
		|				ВТ_ГоловнаяОрганизация.ГоловнаяОрганизация
		|			ИЗ
		|				ВТ_ГоловнаяОрганизация)
		|	И НЕ ЮрЛица.Ссылка = &ЮрЛицо
		|
		|ОБЪЕДИНИТЬ
		|
		|ВЫБРАТЬ
		|	ВТ_ГоловнаяОрганизация.ГоловнаяОрганизация
		|ИЗ
		|	ВТ_ГоловнаяОрганизация КАК ВТ_ГоловнаяОрганизация";
	
	Запрос.УстановитьПараметр("ЮрЛицо", ЮрЛицо);
	
	РезультатЗапроса = Запрос.Выполнить();
	
	Возврат РезультатЗапроса.Выгрузить();
	
КонецФункции

&НаСервере
Процедура ИзменитьИерархию(ЮрЛицо,Филиалы)
	
	ИзменитьГоловнуюОрганизацию(ЮрЛицо, Неопределено);
	ПоставитьФилиалыВПодчинение(ЮрЛицо,Филиалы);
	
КонецПроцедуры

&НаСервере
Процедура ИзменитьГоловнуюОрганизацию(ЮрЛицо, ГоловнаяОрганизация)
	
	ЮрЛицоОбъект = ЮрЛицо.ПолучитьОбъект();
	ЮрЛицоОбъект.ГоловнаяОрганизация = ГоловнаяОрганизация;
	ЮрЛицоОбъект.Записать();
	
КонецПроцедуры

&НаСервере
Процедура ПоставитьФилиалыВПодчинение(ЮрЛицо,Филиалы)
	
	Для Каждого СтрокаТЗ Из Филиалы Цикл
		
		ИзменитьГоловнуюОрганизацию(СтрокаТЗ.Филиал, ЮрЛицо);
		
	КонецЦикла;
	
КонецПроцедуры